apply plugin: 'jacoco'

class CodeCoveragePlugin implements Plugin<Project> {

    void apply(Project project) {
        def execFiles = project.fileTree(project.buildDir).include("/jacoco/*.exec")

        project.jacocoTestReport {
            executionData(execFiles)
            reports { xml.enabled = true }
        }

        project.jacocoTestCoverageVerification {
            executionData(execFiles)
            violationRules { rule { limit {
                counter = 'LINE'
                minimum = 0.80
            } } }
        }

        configureDependsOn(project, 'check', 'jacocoTestReport')
        configureDependsOn(project, 'check', 'jacocoTestCoverageVerification')
    }

    private static configureDependsOn(Project project, String dependentTaskName, String sourceTaskName) {
        def dependentTask = getTask(project, dependentTaskName)
        def sourceTask = getTask(project, sourceTaskName)
        dependentTask.dependsOn sourceTask
    }

    private static getTask(Project project, String name) {
        return project
            .tasks
            .matching { it.name == name } [0]
    }

}

apply plugin: CodeCoveragePlugin
